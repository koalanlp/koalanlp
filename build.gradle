plugins {
    id 'java'
    id 'jacoco'
    id "org.jetbrains.kotlin.jvm" version "1.2.71"
    id "org.jetbrains.dokka" version "0.9.16"
    id "com.bmuschko.nexus" version "2.3.1"
    id "io.codearte.nexus-staging" version "0.12.0"
}

allprojects {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/spekframework/spek-dev" }
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://jitpack.io" }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 10, 'minutes'
        }
    }
}

ext.nexusUsername = properties.'ossrhUsername' as String ?: 'Foo'
ext.nexusPassword = properties.'ossrhPassword' as String ?: 'Foo'

// Dokka aggregation을 위한 library
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

// Dokka aggregation
dokka {
    moduleName = "$rootProject.name"
    outputDirectory = "$rootDir/docs/api"
    outputFormat = "html"
    jdkVersion = 8
    noStdlibLink = false

    doFirst {
        sourceDirs = files(subprojects.collect { p ->
            def path = new File(p.projectDir, "/src/main/kotlin")

            def relativePath = rootDir.toPath().relativize(path.toPath()).toString()
            linkMapping {
                dir = path
                url = "https://github.com/koalanlp/koalanlp/blob/master/blob/master/$relativePath"
                suffix = "#L"
            }

            return path
        })
        
        includes = ['docs/index.md', *subprojects.collect { "${it.projectDir}/index.md" }]
        
        file(outputDirectory).deleteDir()
    }
}

// Test Report Aggregation
jacoco {
    toolVersion = "0.8.2"
}

task aggregateTestReport(type: JacocoReport) {
    sourceDirectories = files()
    classDirectories = files()
    executionData = files()

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    onlyIf = {
        true
    }

    doFirst {
        subprojects.findAll { subproject ->
            subproject.pluginManager.hasPlugin('java')
        }.each { subproject ->
            additionalSourceDirs files((Set<File>) subproject.sourceSets.main.allJava.srcDirs)
            additionalClassDirs ((FileCollection) subproject.sourceSets.main.output)
            if (subproject.pluginManager.hasPlugin('jacoco')) {
                executionData subproject.tasks.jacocoTestReport.executionData
            }
        }

        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}